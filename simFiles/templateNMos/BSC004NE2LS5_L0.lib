
.SUBCKT BSC004NE2LS5_L0 drain gate source

Lg     gate  g1    2.2n
Ld     drain d1    100p
Ls     source s1   350p
Rs     s1     s2   159u

Rg     g1     g2   0.7

M1     d2     g2    s2    s2    DMOS    L=1u   W=1u
.MODEL DMOS NMOS ( KP=1707  VTO=2.18  THETA=0  VMAX=1.5e5  ETA=0.01  LEVEL=3)
Rd     d1    d2    0.16m  TC=7m

Dbd     s2    d2    Dbt
.MODEL     Dbt    D(BV=30  M=0.8  CJO=2.28n  VJ=2.3V)
Rsp     s2    s3     0.5
Dbd1    s3    d2     Dbt1
.MODEL     Dbt1    D(BV=1000   M=0.8  CJO=1.35n  VJ=2.3V)
Dbody   s2   21    DBODY
.MODEL DBODY  D(IS=96.7p  N=1.12  RS=0.04u  EG=1.12  TT=3n)
Rdiode  d1  21    0.18m  TC=10m

.MODEL   sw    NMOS(VTO=0  KP=10   LEVEL=1)
Maux      g2   c    a    a   sw
Maux2     b    d    g2    g2   sw
Eaux      c    a    d2    g2   1
Eaux2     d    g2   d2    g2   -1
Cox       b    d2   10.81n
.MODEL     DGD    D(M=0.36  CJO=10.81n  VJ=0.5)
Rpar      b    d2   1Meg
Dgd       a    d2   DGD
Rpar2     d2   a    10Meg
Cgs     g2    s2    7.74n

.ENDS BSC004NE2LS5_L0